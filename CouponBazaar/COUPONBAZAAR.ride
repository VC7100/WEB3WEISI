{-# STDLIB_VERSION 5 #-}
{-# CONTENT_TYPE DAPP #-}
{-# SCRIPT_TYPE ACCOUNT #-}


let accountPubKey = base58'5AzfA9UfpWVYiwFwvdr77k6LWupSTGLb14b24oVdEpMM'
let alicePubKey = base58'2KwU4vzdgPmKyf7q354H9kSyX9NZjNiq4qbnH2wi2VDF'

@Callable(i)
func purchase(name: String) = {
  if(size(i.payments) > 0) then {
        let payment = getElement(i.payments, 0)
    if(isDefined(name)) then {
        if(isDefined(payment.assetId)) then throw("Only should pay in waves")
          else {
            let customerAddr = toBase58String(i.caller.bytes)
            let price = match getInteger(this, "item_" + name + "_coupon_price") {
                case t:Int => t
                case _ => 0
              }
            if(price != 0) then {
              if(payment.amount > price) then throw("payment cant be higher than price: " + price.toString())
              else if(payment.amount < price) then throw("payment cant be less than price: " + price.toString())
              else {
                [
                  StringEntry("status:purchase_item_" + name + "_customer_" + customerAddr, "confirmed"),
                  IntegerEntry("price:purchase_item_" + name + "_customer_" + customerAddr, price)
                ]
              }
            } else throw("Please enter valid coupon name: A or B")
          }
    } else throw("Coupon name should be present")
  } else throw("Please attach payment")
}

@Callable(i)
func setPrices() = {  #hardcoded setting price for coupons
  if(i.callerPublicKey == accountPubKey) then{
    [
    IntegerEntry("item_A_coupon_price", 28000000),
    IntegerEntry("item_B_coupon_price", 32000000)
  ]
  } else {
    throw("Only owner can set prices")
  }
}

@Callable(i)
func deleteEntry(key: String) = { #utility function for deleting entries while testing
  if(i.callerPublicKey == accountPubKey) then {
    [
      DeleteEntry(key)
    ]
  } else {
    throw("Only owner can delete entries")
  }
}

func checkMultiSig(tx: Transaction) = {
  let accSig = if (sigVerify(tx.bodyBytes, tx.proofs[0], accountPubKey)) then 1 else 0
  let aliceSig = if (sigVerify(tx.bodyBytes, tx.proofs[1], alicePubKey)) then 1 else 0
  (accSig + aliceSig) == 2
}

@Verifier(tx)
func verify() = {
  sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey)
}